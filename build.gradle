buildscript { repositories { jcenter() } }

plugins {
    id "org.jetbrains.kotlin.jvm" version "1.2.41"
    id "jacoco"
    id "nl.fabianm.kotlin.plugin.generated" version "1.0" // annotate generated methods to exclude from coverage
    id "me.champeau.gradle.jmh" version "0.4.6"
}

group = 'com.mmdemirbas'
version = '1.0.0'

repositories { jcenter() }

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.2.41"

    testCompile 'org.junit.jupiter:junit-jupiter-api:5.2.0'
    testRuntime 'org.junit.jupiter:junit-jupiter-engine:5.2.0'
    testCompile 'org.junit.jupiter:junit-jupiter-params:5.2.0'

    testCompile 'org.springframework.boot:spring-boot-starter-web:1.3.8.RELEASE'
}

// exclude Junit4
configurations.all { exclude group: 'junit' }

// Generates gradlew[.bat] scripts
task wrapper(type: Wrapper) {
    gradleVersion = '4.7'
}

// Kotlin configuration
compileKotlin { kotlinOptions { jvmTarget = "1.8" } }
compileTestKotlin { kotlinOptions { jvmTarget = "1.8" } }

// Jacoco & test configuration
test {
    useJUnitPlatform()
    testLogging { events "passed", "skipped", "failed" }
    reports { html.enabled = true }
    finalizedBy jacocoTestReport
}
jacocoTestReport {
    reports {
        html.enabled = true
        xml.enabled = true
    }
}
check.dependsOn jacocoTestReport

// JMH configuration
sourceSets {
    jmh {
        compileClasspath += sourceSets.test.runtimeClasspath
        runtimeClasspath += sourceSets.test.runtimeClasspath
    }
}
jmh {
    warmupIterations = 10
    warmupForks = 1
    warmup = '1s'
    iterations = 3
    timeOnIteration = '1s'
    timeout = '1s'
    threads = 3
    benchmarkMode = ['all']
}
