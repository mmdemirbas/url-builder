buildscript {
    ext.kotlin_version = '1.2.41'
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.3.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'groovy'
apply plugin: 'kotlin'
apply plugin: 'findbugs'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    testCompile 'junit:junit:4.12'
}

task sourceJar(type: Jar, dependsOn: classes) {
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
}


group = 'com.palominolabs.http'
version = '1.1.2-SNAPSHOT'
ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

// build a pom
publishing {
    publications {
        bintray(MavenPublication) {
            from components.java
            groupId project.group
            artifactId project.name
            version project.version

            artifact sourceJar {
                classifier 'sources'
            }

            artifact javadocJar {
                classifier 'javadoc'
            }
        }
    }
}

bintray {
    user = rootProject.hasProperty('bintrayUser') ? rootProject.property('bintrayUser') : 'FIXME'
    key = rootProject.hasProperty('bintrayApiKey') ? rootProject.property('bintrayApiKey') : 'FIXME'
    publications = ['bintray']

    pkg {
        repo = 'maven'
        licenses = ['Copyfree']
        vcsUrl = 'https://github.com/palominolabs/url-builder'
        name = 'com.palominolabs.http:url-builder'

        version {
            name = project.version
            released = new Date()
            if (project.isReleaseVersion) {
                vcsTag = 'v' + project.version
            }
        }
    }
}

findbugs {
    toolVersion = '3.0.1'
    // don't findbugs the tests
    sourceSets = [sourceSets.main]
}

tasks.withType(FindBugs) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

repositories {
    mavenCentral()
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
